#!/usr/bin/perl -w
use strict;
use Data::Dump qw/dd/;
use File::Path qw(make_path);
use YAML;
use TBBTestSuite::Common qw(exit_error);
use TBBTestSuite::Options qw($options);
use TBBTestSuite::Reports;
use TBBTestSuite::Tests;

sub run_tests {
    return unless @{$options->{args}};
    my $report = {
        tbbfiles => {},
        options  => $options,
        time     => time,
        system_infos => TBBTestSuite::Common::system_infos,
        report_format => '1',
    };
    TBBTestSuite::Reports::set_report_dir($report);
    foreach my $tbbfile (@{$options->{args}}) {
        TBBTestSuite::Tests::test_tbb($report, $tbbfile);
    }
    print "Report directory: $options->{'report-dir'}\n";
    YAML::DumpFile("$options->{'report-dir'}/report.yml", $report);
    TBBTestSuite::Reports::make_report($report);
    TBBTestSuite::Reports::make_reports_index;
}

sub make_report {
    exit_error "--name is not set" unless $options->{name};
    my $report = { options => $options };
    TBBTestSuite::Reports::set_report_dir($report);
    $report = YAML::LoadFile("$options->{'report-dir'}/report.yml");
    $report->{options} = { %{$report->{options}}, %$options };
    TBBTestSuite::Reports::make_report($report);
}

my %actions = (
    run_tests => \&run_tests,
    list_tests => \&TBBTestSuite::Tests::list_tests,
    reports_index => \&TBBTestSuite::Reports::make_reports_index,
    make_report => \&make_report,
);

exit_error "Unknow action $options->{action}" unless $actions{$options->{action}};
make_path($options->{tmpdir});
$actions{$options->{action}}->();
