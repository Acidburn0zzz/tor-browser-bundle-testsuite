#!/usr/bin/perl -w
use strict;
use FindBin;
use lib "$FindBin::Bin";
use Data::Dump qw/dd/;
use File::Path qw(make_path);
use YAML;
use TBBTestSuite::Common qw(exit_error run_alone rm_pidfile);
use TBBTestSuite::Options qw($options);
use TBBTestSuite::Reports;
use TBBTestSuite::Reports::Receive;
use TBBTestSuite::Reports::Send;
use TBBTestSuite::Tests;

sub set_report_success {
    my ($report) = @_;
    $report->{success} = 1;
    $report->{failed_tests} = {};
    foreach my $tbbfile_name (keys %{$report->{tbbfiles}}) {
        my $tbbfile = $report->{tbbfiles}{$tbbfile_name};
        next if $tbbfile->{success};
        $report->{success} = 0;
        foreach my $test (@{$tbbfile->{tests}}) {
            if ($test->{results} && !$test->{results}{success}) {
                push @{$report->{failed_tests}{$test->{name}}}, $tbbfile_name;
            }
        }
    }
}

sub run_tests {
    return unless @{$options->{args}};
    my $report = {
        tbbfiles => {},
        options  => $options,
        time     => time,
        system_infos => TBBTestSuite::Common::system_infos,
        report_format => '1',
    };
    TBBTestSuite::Reports::set_report_dir($report);
    foreach my $tbbfile (@{$options->{args}}) {
        my $tbbinfos = TBBTestSuite::Tests::tbb_filename_infos($tbbfile);
        TBBTestSuite::Tests::test_tbb($report, $tbbinfos);
    }
    set_report_success($report);
    print "Report directory: $options->{'report-dir'}\n";
    YAML::DumpFile("$options->{'report-dir'}/report.yml", $report);
    TBBTestSuite::Reports::make_report($report);
    TBBTestSuite::Reports::make_reports_index;
    TBBTestSuite::Reports::Send::send_report if $options->{'upload-to'};
}

sub load_report_from_file {
    exit_error "--name is not set" unless $options->{name};
    my $report = { options => $options };
    TBBTestSuite::Reports::set_report_dir($report);
    $report = YAML::LoadFile("$options->{'report-dir'}/report.yml");
    $report->{options} = { %{$report->{options}}, %$options };
    return $report;
}

sub make_report {
    my $report = load_report_from_file;
    TBBTestSuite::Reports::make_report($report);
}

sub receive_report {
    TBBTestSuite::Reports::Receive::receive_report;
    TBBTestSuite::Reports::make_reports_index;
}

sub email_report {
    my $report = load_report_from_file;
    TBBTestSuite::Reports::email_report($report);
}

sub show_report {
    my $report = load_report_from_file;
    print TBBTestSuite::Reports::text_report($report);
}

sub dump_options {
    dd $options;
}

my %actions = (
    run_tests => \&run_tests,
    list_tests => \&TBBTestSuite::Tests::list_tests,
    reports_index => \&TBBTestSuite::Reports::make_reports_index,
    make_report => \&make_report,
    send_report => \&TBBTestSuite::Reports::Send::send_report,
    receive_report => \&receive_report,
    update_authkeys => \&TBBTestSuite::Reports::Receive::update_authkeys,
    email_report => \&email_report,
    show_report  => \&show_report,
    dump_options => \&dump_options,
);

sub usage {
    print STDERR "$0 [--action=action_name] [OPTIONS...]\n\n";
    print STDERR "Possible actions:\n";
    foreach my $act (keys %actions) {
        print STDERR " - $act\n";
    }
    exit 1;
}

usage unless @ARGV;
exit_error "Unknow action $options->{action}" unless $actions{$options->{action}};
run_alone;
make_path($options->{tmpdir});
$actions{$options->{action}}->();
rm_pidfile;
